plugins {
  id "com.jfrog.bintray" version "1.1"
}

apply plugin: 'scala'
apply plugin: 'application'
apply plugin: 'maven-publish'

repositories {
  mavenCentral()
}

ext {
  SCALA_VERSION = "2.11.6"
  GATLING_VERSION = "2.1.4"
}

dependencies {
  compile "org.scala-lang:scala-library:${SCALA_VERSION}"

  compile "io.spray:spray-client_2.11:1.3.2"
  compile "io.spray:spray-json_2.11:1.3.1"
  compile "com.typesafe.akka:akka-actor_2.11:2.3.2"
  compile "ch.qos.logback:logback-classic:1.1.2"
  compile "com.typesafe.scala-logging:scala-logging_2.11:3.1.0"
  compile "com.typesafe:config:1.2.1"
  compile "org.threeten:threetenbp:1.2"

  testCompile 'junit:junit:4.12'
  testCompile 'org.scalatest:scalatest_2.11:2.2.4'
}

def applicationJvmArgs = []
['server-url', 'username', 'password', 'active-releases', 'completed-releases', 'templates'].each {
  if (project.hasProperty(it)) {
    applicationJvmArgs.add("-Dxl.data-generator.$it=${project.property(it)}")
  }
}

run {
  jvmArgs applicationJvmArgs
}

mainClassName = "com.xebialabs.xlrelease.Main"


/* Project publication details */

def pomConfig = {
  name 'XL Release Data Generator'
  url project_url
  scm { url project_scm }

  organization {
    name 'XebiaLabs'
    url 'http://www.xebialabs.com'
  }
}

publishing {
  publications {
    projectJar(MavenPublication) {
      from components.java

      groupId project_group_id
      artifactId project_artifact_id

      pom.withXml {
        asNode().appendNode('description', project_description)
        asNode().children().last() + pomConfig
      }
    }
  }
}

bintray {
  user = bintrayUsername
  key = bintrayApiKey

  publications = ['projectJar']
  dryRun = false
  publish = false
  pkg {
    repo = 'maven'
    userOrg = 'xebialabs'
    name = project_artifact_id
    desc = project_description
    websiteUrl = project_url
    issueTrackerUrl = project_issues_url
    vcsUrl = project_scm
    licenses = ['Apache-2.0']
    labels = ['xlrelease', 'release', 'xebialabs', 'xl', 'continuous delivery', 'cd']
    publicDownloadNumbers = true
  }
}
